plugins {
    id 'eclipse'
    id 'java'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '1.0.21'
}

version = '1.0.0'
group = 'com.github.dustskys'
base {
    archivesName = 'PeppaPigIsATrowel'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = '21.1.94'

    mods {
        peppapig {
            sourceSet sourceSets.main
        }
    }

    parchment {
        // https://parchmentmc.org/docs/getting-started
        // https://ldtteam.jfrog.io/ui/native/parchmentmc-public/org/parchmentmc/data/
        minecraftVersion = '1.21.1'
        mappingsVersion = '2024.11.17'
    }

    runs {
        client {
            client()
            ideName = 'runPeppaPigClient'
        }

        server {
            server()
            ideName = 'runPeppaPigServer'
        }
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
            'Specification-Title': 'Peppa Pig is a Trowel',
            'Specification-Vendor': 'Dustskys',
            'Specification-Version': '1',
            'Implementation-Title': project.name,
            'Implementation-Version': "${version}",
            'Implementation-Vendor': 'Dustskys'
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.named('processResources', ProcessResources).configure {
    doLast {
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
            File file -> file.text = groovy.json.JsonOutput.toJson(new groovy.json.JsonSlurper().parse(file))
        }
    }
}